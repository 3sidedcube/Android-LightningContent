plugins {
	id 'com.android.library'
}

repositories {
	maven { url "https://oss.jfrog.org/libs-snapshot" }
}

android {
	compileSdkVersion rootProject.ext.compileSdkVersion

	defaultConfig {
		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
	}

	lintOptions {
		abortOnError false
	}
	compileOptions {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}
}

dependencies {
	implementation 'androidx.annotation:annotation:1.3.0'
	implementation 'androidx.work:work-runtime:2.7.1'
	implementation 'androidx.work:work-rxjava2:2.7.1'

	implementation 'com.3sidedcube.storm:util:1.2.0'

	implementation 'io.reactivex.rxjava2:rxjava:2.2.19'
	implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

	implementation 'com.google.code.gson:gson:2.8.6'
	implementation 'net.callumtaylor:asynchttpclient:2.1'
	implementation 'org.kamranzafar:jtar:2.3'
	implementation 'com.jakewharton.timber:timber:4.7.1'

	compileOnly 'org.projectlombok:lombok:1.18.16'
	annotationProcessor 'org.projectlombok:lombok:1.18.16'
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

allprojects {
	tasks.withType(Javadoc) {
		options.addStringOption('Xdoclint:none', '-quiet')
	}
}

ext.ARTIFACTORY_USERNAME = properties.get('ARTIFACTORY_USERNAME', '')
ext.ARTIFACTORY_PASSWORD = properties.get('ARTIFACTORY_PASSWORD', '')

afterEvaluate { project ->
	publishing {
		publications {
			mavenJava(MavenPublication) {
				from components.release
				groupId = GROUP
				artifactId = POM_ARTIFACT_ID
				version = VERSION_NAME

				pom {
					name = POM_NAME
					packaging = POM_PACKAGING
					description = POM_DESCRIPTION
					url = POM_URL

					scm {
						url = POM_SCM_URL
						connection = POM_SCM_CONNECTION
						developerConnection = POM_SCM_DEV_CONNECTION
					}

					licenses {
						license {
							name = POM_LICENSE_NAME
							url = POM_LICENSE_URL
							distribution = 'repo'
						}
					}

					developers {
						developer {
							id = POM_DEVELOPER_ID
							name = POM_DEVELOPER_NAME
						}
					}
				}
			}
		}
		repositories {
			maven {
				url = "http://oss.3sidedcube.com:8081/artifactory/internal"
				allowInsecureProtocol = true
				credentials {
					username = ARTIFACTORY_USERNAME
					password = ARTIFACTORY_PASSWORD
				}
			}
		}
	}

	signing {
		sign publishing.publications.mavenJava
	}
}
